plugins {
    id "org.sonarqube" version "2.5"
}

group 'com.github.mictaege'
version '2018.8'

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

repositories {
    mavenCentral()
    mavenLocal()
}

sonarqube {
    properties {
        property "sonar.projectName", "com.github.mictaege.jitter-api"
        property "sonar.projectKey", "com.github.mictaege.jitter-api"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") } //sign only when upload archives
    sign configurations.archives
}

//don't refer to username and pwd directly so it could be executed in another environment with another _gradle.properties_
String publishUsername = hasProperty("ossrhUsername") ? property("ossrhUsername") : ""
String publishPassword = hasProperty("ossrhPassword") ? property("ossrhPassword") : ""

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            pom.project {
                name 'jitter-api'
                packaging 'jar'
                description 'This repository contains the _jitter_ API. For using _jitter_ see the https://github.com/mictaege/jitter_plugin documentation.'
                url 'https://github.com/mictaege/jitter-api'
                scm {
                    connection 'https://github.com/mictaege/jitter-api.git'
                    url 'https://github.com/mictaege/jitter-api'
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'mictaege'
                        name 'Michael Taege'
                    }
                }
            }
        }
    }
}
